//NOW WE ARE IN THE ZONE IV THE BLOCKS

title: TheBlocks
---
<<fadeIn>>
//variables for Unity
<<set $entertheantique to false>> // enter antique shop
<<set $trigger_TheAllKnowingLady to false>> //trigger to the Dreamworld, but inside the dialogue of TheAllKnowingLady
<<set $putintheinventory003 to false>> //this is for the physical object in the Antique shop, used in the 3rd DREAMWORLD
<<set $exittheantique to false>> //exit antique shop
<<set $poetsdiary to false>> //shows an illustration of The Lonely Gentleman's diary (maybe we can make it interactive so you can turn some pages)
<<set $putintheinventory008 to false>> //an object from the Lonely Gentleman
<<set $coinisselected to false>>

//variables for in YARN
<<set $talkedaboutrabbits to false>>
<<set $haspoetclue to false>>
<<set $clue_uncle to false>>
<<fadeOut>>

<<SetPortrait noportrait>>
Magnolia: (I have nothing to lose.)
Magnolia: (I have nothing to lose, I have nothing to lose...)
Magnolia: (Even if I AM crazy, it doesn't matter, right?)
Magnolia: (This whole city is crazy!)
Magnolia: (And I have no family, so there is nobody to judge me for being completely nuts.)
Magnolia: (Also...)
Magnolia: (That felt absolutely real.)
Magnolia: (I just hate that I always wake up.)
Magnolia: (Dreams seem to work like that.)
Magnolia: (Just when the excitement is at its peak, right before the big revelation...)
Magnolia: (It disappears.)
Magnolia: (I need to find a way to go back.)
//new objective: find a way to go to the Dreamworld.
===

title: TheCarCleaner
---
<<if visited_count("TheCarCleaner") < 1>>
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: All these rabbits leaving paw prints over the cars.
 The Car Cleaner: It's killing me.
 <<SetPortrait noportrait>>
 Magnolia: They were purple, weren't they?
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: Yes, very purple.
 The Car Cleaner: Purple is a stubborn color.
 <<SetPortrait noportrait>>
 Magnolia: Are you a car cleaner?
 Magnolia: I don't see a shop anywhere near?
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: I'm a freelancer.
 The Car Cleaner: I hope to buy myself one of these babies one day.
 <<SetPortrait noportrait>>
 Magnolia: How long do you need to work?
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: I calculated...
 The Car Cleaner: It would take me exactly 82125 cars to clean...
 The Car Cleaner: Or 75 years, counting 3 cars per day.
 The Car Cleaner: It's doable.
  <<jump TheCarCleaner_Questions>>
<<else>>
 The Car Cleaner: You seem like you want to say something.
 <<jump TheCarCleaner_Questions>>
<<endif>>
===

title: TheLonelyGentleman
---
<<if visited_count("TheLonelyGentleman") < 1>>
 <<SetPortrait noportrait>>
 Magnolia: (He seems approachable.)
 Magnolia: Have you noticed anything strange around here?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I observe only people.
 The Lonely: And they are always so strange to me.
 <<SetPortrait noportrait>>
 Magnolia: Nothing too out of the ordinary?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: Except for a child carrying a goat in its arms, not really.
 <<SetPortrait noportrait>>
 Magnolia: A goat?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: It seemed to be bothered by the sun.
 The Lonely: Either it was blinking, or winking at me.
 <<SetPortrait noportrait>>
 Magnolia: (If it's possible, the sky is even grayer now.)
 <<wait 1>>
 Magnolia: I saw you writing something.
 Magnolia: What is it?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I record people which pass by me.
 <<SetPortrait noportrait>>
 Magnolia: How do you record them?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I write their descriptions.
 The Lonely: And their life stories.
 <<SetPortrait noportrait>>
 Magnolia: How could you know their life stories?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: Well, I give my best bet.
 The Lonely: There is nobody to disapprove of me.
 <<SetPortrait noportrait>>
 Magnolia: That seems fun.
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: This is not fun for me.
 The Lonely: It makes me feel lonely.
 <<SetPortrait noportrait>>
 Magnolia: How so?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: They all have lives to go to.
 <<SetPortrait noportrait>>
 Magnolia: And you?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I can't leave this lamp.
 <<SetPortrait noportrait>>
 Magnolia: Why?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I am waiting for someone.
 <<SetPortrait noportrait>>
 Magnolia: Can I ask who.
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: Gogo and Didi.
 The Lonely: My childhood friends.
 The Lonely: They are a little bit late.
 <<wait 0.5>>
 <<SetPortrait noportrait>>
 Magnolia: (This guy seems like he has seen stuff.)
 Magnolia: (He could be useful.)
  <<jump TheLonelyGentleman_Questions>>
<<elseif visited_count("TheLonelyGentleman") == 1>>
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: It's again you!
 <<SetPortrait noportrait>>
 Magnolia: What do you mean?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I turn to your page so many times.
 The Lonely: Are you curious about anything?
 <<jump TheLonelyGentleman_Questions>>
<<else>>
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I have all the time in this world.
 <<jump TheLonelyGentleman_Questions>>
<<endif>>
===

title: TheLonelyGentleman_Questions
---
-> (I suddenly feel too shy to ask him anything.)
-> How long have you been waiting?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: It's been sometime now.
 <<SetPortrait noportrait>>
 Magnolia: And that is? 
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I don't count.
 <<SetPortrait noportrait>>
 Magnolia: But you have a watch on your arm.
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: It's beyond this watch.
 <<SetPortrait noportrait>>
 Magnolia: You mean it's more than a day?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I would guess it's more than a year.
 The Lonely: But I can't be sure.
 <<SetPortrait noportrait>>
 Magnolia: Why would you wait so long?
 <<wait 0.5>>
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I didn't plan to wait this long.
 <<SetPortrait noportrait>>
 Magnolia: Of course, you didn't. 
 Magnolia: But I think they stood you up.
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I don't care if they show up or not.
 <<SetPortrait noportrait>>
 Magnolia: How can you not care? You are waiting for them!
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: Yes, at the beginning I did wait for them.
 The Lonely: But after, I started waiting for myself.
 <<SetPortrait noportrait>>
 Magnolia: In which sense?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: In its all meanings.
 The Lonely: I started to enjoy waiting.
 The Lonely: But also, I might meet myself on the street. Who knows.
 <<SetPortrait noportrait>>
 Magnolia: Oh okay.
 <<wait 0.5>>
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: You could wait with me if you want.
 <<SetPortrait noportrait>>
 Magnolia: For what?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: For whatever you want.
 <<SetPortrait noportrait>>
 Magnolia: I don't think waiting will bring me what I want.
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: Then I wish you luck wherever you are going.
 <<SetPortrait noportrait>>
 Magnolia: Thank you.
-> Could I look into your diary? <<if $haspoetclue is true>>
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I don’t think you would understand.
 <<SetPortrait noportrait>>
 Magnolia: Maybe you could give me a chance?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: My words are shy.
 The Lonely: Especially with people who are prone to judge them.
 <<SetPortrait noportrait>>
 Magnolia: I swear I won't judge!
 <<wait 0.5>>
 Magnolia: I found this one note on the street.
 Magnolia: I thought it was beautiful.
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: What is it?
 <<SetPortrait noportrait>>
 Magnolia: [i]I mistake the rain for your tears.[/i]
 Magnolia: [i]And on those days just before it rains, I feel like you are close.[/i]
 <<wait 0.5>>
 Magnolia: Isn't it beautiful?
 <<wait 0.5>>
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: It's a little bit too romantic for my taste.
 The Lonely: Who wrote it?
 <<SetPortrait noportrait>>
 Magnolia: I don't know. I found it on the street.
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: Well that's more to my taste.
 The Lonely: Invisible poets.
 The Lonely: Lost words.
 <<wait 1>>
 <<SetPortrait noportrait>>
 Magnolia: You still don't want to show me?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I could give you a glimpse.
 <<SetPortrait noportrait>>
 Magnolia: Thank you, thank you!
 <<set $poetsdiary to true>>
 <<wait 5>>
 <<jump TheDiary>>
 <<SetPortrait noportrait>>
-> Did you see me before?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: I did. 
 The Lonely: But you looked different each time.
 <<SetPortrait noportrait>>
 Magnolia: How different?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: We all look very different every day.
 <<SetPortrait noportrait>>
 Magnolia: But I'm sure it's not THAT different.
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: Oh, but it is.
 The Lonely: Even the sun is never the same.
 <<SetPortrait noportrait>>
 Magnolia: It looks pretty much the same to me!
 Magnolia: Except when the clouds cover it, or when it's a winter day, so it's barely there.
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: But that's exactly what I am talking about.
 The Lonely: You never got clouds covering your being?
 <<SetPortrait noportrait>>
 Magnolia: Uhm...
 Magnolia: You mean like depression?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: It could be that.
 The Lonely: Or something else.
 The Lonely: But it changes a person completely.
 <<SetPortrait noportrait>>
 Magnolia: I never looked at it like that.
 <<wait 0.5>>
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: Here, I have something for you.
 <<SetPortrait noportrait>>
 Magnolia: What is it?
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: An old cigarette tin.
 The Lonely: I don't smoke, so it's no use for me.
 The Lonely: However its cover nicely reflects the image of your face.
 The Lonely: I think you should have it.
 The Lonely: You could see how you change.
 <<SetPortrait noportrait>>
 Magnolia: I don’t know what to say.
 Magnolia: That's so nice of you.
 <<SetPortrait lonelygentelmanportrait1>>
 The Lonely: Thank you is enough.
 <<SetPortrait noportrait>>
  -> Take the gift.
   <<set $putintheinventory008 to true>> //OBJECT 008
   <<SetPortrait noportrait>>
   Magnolia: Thank you!
   Magnolia: (I don't think I am going to use it tho.)
   Magnolia: (I don't like seeing myself.)
   Magnolia: (It reminds me I am still alive.)
===

title: TheDiary
---
<<SetPortrait noportrait>>
Magnolia: (He has seen some stuff.)
Magnolia: (Too bad he didn't show me all of it.)
===

title: TheCarCleaner_Questions
---
-> Why only three cars per day?
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: There is no room for mistakes, that's why.
 <<SetPortrait noportrait>>
 Magnolia: I thought you would say you need rest time.
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: That's the one thing I don't need.
 The Car Cleaner: My uncle used to say that we can rest when we die.
 <<wait 0.5>>
 <<SetPortrait noportrait>>
 Magnolia: What is your uncle doing now?
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: He died when he was 41.
 <<SetPortrait noportrait>>
 Magnolia: That's...
 Magnolia: That's too young.
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: Yeah.
 The Car Cleaner: Shame he didn't get to finally buy one of those boats he used to work on.
 <<wait 0.5>>
 <<SetPortrait noportrait>>
 Magnolia: That's a shame...
 <<set $clue_uncle to true>>
-> Aren't you afraid you are going to die as early as your uncle? <<if $clue_uncle is true>>
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: Why would I?
 The Car Cleaner: Are you cursing me or something?
 <<SetPortrait noportrait>>
 Magnolia: No!
 Magnolia: I just think his health might have been influenced by the way he led his life.
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: What do you mean?
 <<SetPortrait noportrait>>
 Magnolia: "I'll rest when I die?"
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: No, you are mistaken.
 The Car Cleaner: The doctors said he died of a heart attack.
 The Car Cleaner: It just stopped working.
 The Car Cleaner: Nothing we can do about that.
 <<SetPortrait noportrait>>
-> 75 years is a long time.
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: But it will be worth it!
 <<wait 0.5>>
 <<SetPortrait noportrait>>
 Magnolia: How old did you say you were?
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: Thirty-one.
 <<SetPortrait noportrait>>
 Magnolia: I see.
 Magnolia: You never really liked math, did you?
 <<SetPortrait carcleanerportrait1>>
 The Car Cleaner: I don't have time for such academic stuff you know.
 <<SetPortrait noportrait>>
 Magnolia: That's okay.
 Magnolia: Good luck!
-> I'll let you do your thing.
===

title: TheBlobPerson
---
//the blob person and the husband have the connection
//you unlock a part of his conversation from a scrunched drawing of a kid you found
<<if visited_count("TheBlobPerson") < 1>>
 <<SetPortrait noportrait>>
 Magnolia: (He looks a little bit strange.)
 Magnolia: (Maybe he knows something.)
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: They are hiding in the shadows of the sun.
 <<SetPortrait noportrait>>
 Magnolia: Who?
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: It's almost like I can hear them from inside my own body.
 <<SetPortrait noportrait>>
 Magnolia: (A little too strange.)
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: I am prosecuted!
 The Blob Person: Don't you understand?!
 <<SetPortrait noportrait>>
 Magnolia: I might be able to help if you told me who is chasing you.
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: I don't see the faces of people anymore. //possibly A CLUE
 The Blob Person: They took my body away.
 The Blob Person: Those demons.
  <<jump TheBlobPerson_Questions02>>
<<elseif visited_count("TheBlobPerson") == 1>>
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: I know you don't believe me.
 The Blob Person: Nobody believes a person without a body.
 <<wait 0.5>>
 <<SetPortrait noportrait>>
 Magnolia: That's not true...
 Magnolia: What I meant to say is, I believe you.
 Magnolia: (At least I think so.) 
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: Thank you.
 The Blob Person: It...
 The Blob Person: It means a lot.
  <<wait 0.5>>
  <<jump TheBlobPerson_Questions02>>
<<else>>
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: This huge body feels weightless.
  <<jump TheBlobPerson_Questions02>>
<<endif>>
===

title: TheBlobPerson_Questions01
---
<<SetPortrait noportrait>>
Magnolia: (I don't have time to deal with his problems.)
Magnolia: (I need to find out how to go back.)
 -> Did you see anything unusual around here? 
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: Except for the monsters prying on me, not really.
  <<SetPortrait noportrait>>
  Magnolia: How do these monsters look like?
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: Like you and me.
  <<SetPortrait noportrait>>
  Magnolia: No pointy ends? 
  Magnolia: Stardust around them?
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: Nothing like that.
  The Blob Person: You sound a little bit strange.
  <<SetPortrait noportrait>>
  Magnolia: Yes, ok.
  Magnolia: (He doesn't know anything.)
   <<jump TheBlobPerson_Questions02>>
 -> Have you ever controlled your dreams?
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: When I was a kid yes.
  The Blob Person: I would lead a lot of epic battles inside my dreams.
  The Blob Person: But we live in reality unfortunately.
  The Blob Person: And it weights down on us.
  The Blob Person: This body is suffocating.
   <<jump TheBlobPerson_Questions02>>
===

title: TheBlobPerson_Questions02
---
<<SetPortrait noportrait>>
Magnolia: (I don't think he is going to talk about anything except himself.)
Magnolia: (He seems stuck.)
Magnolia: (Something about his own body bothers him.)
-> (But I don't want to ask him anything.)
-> Where did they take your body?
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: They hid it underneath.
 <<SetPortrait noportrait>>
 Magnolia: Underneath what?
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: This skin. Do you see?
 <<SetPortrait noportrait>>
 Magnolia: Yes?
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: It's upside down.
 <<SetPortrait noportrait>>
 Magnolia: I don't think I understand.
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: Nobody does until it's too late.
-> How does your body look like?
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: It's an average-looking body.
 The Blob Person: It has two legs and two arms.
 <<SetPortrait noportrait>>
 Magnolia: Anything more specific?
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: It has scars from the pebbles from when I was a kid.
 <<SetPortrait noportrait>>
 Magnolia: How come your body doesn't have them anymore?
 <<SetPortrait blobpersonportrait1>>
 The Blob Person: It's upside down.
 The Blob Person: It's only a memory now.
 <<SetPortrait noportrait>>
-> Show the drawing. <<if $putintheinventory001 is true>> 
 <<SetPortrait noportrait>>
 Magnolia: (I need to take it out of my backpack.) //The player now needs to open the inventory.
===

title: TheBlobPerson_Drawing
---
<<SetPortrait noportrait>>
Magnolia: I have something to show you.
<<SetPortrait blobpersonportrait1>>
The Blob Person: What is it?
<<SetPortrait noportrait>>
Magnolia: Here. Take it.
<<SetPortrait blobpersonportrait1>>
The Blob Person: It's a child's drawing?
<<wait 0.5>>
The Blob Person: Where did you find this?
<<SetPortrait noportrait>>
Magnolia: In Uphill. 
Magnolia: Somebody threw it away from their window.
 -> Do you think a child threw it away? 
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: It's so sad to think that a kid doesn't love their own drawing.
  <<SetPortrait noportrait>>
  Magnolia: Were you a self-critical child?
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: I was not.
  The Blob Person: But my father was very critical of me.
  <<SetPortrait noportrait>>
  Magnolia: That's too much pressure for a kid.
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: It was okay.
  The Blob Person: Now I have the strength to fight for my body.
  <<SetPortrait noportrait>>
  Magnolia: Your body looks a little bit like those from the drawing.
  Magnolia: Do you see it?
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: They do resemble it.
 -> Do you think an adult threw it away? 
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: I don't think anyone would throw away a child's drawing.
  The Blob Person: It's just so endearing.
  The Blob Person: Don't you think?
  <<SetPortrait noportrait>>
  Magnolia: Yes, I couldn't imagine.
  Magnolia: But look at their bodies.
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: What about them?
  <<SetPortrait noportrait>>
  Magnolia: They all seem to be missing.
  <<SetPortrait blobpersonportrait1>>
  The Blob Person: They are there. They are not missing!
  The Blob Person: It's just that they were drawn by a kid.
===

title: TheDeadbitHusband
---
//he is drunk with a girl under his arm, he is only making a joke after a joke, at a certain joke he asks you to guess the end. 
//To be able to guess the end you would have need to have read a certain text/graffiti(its in the DownHill) that is an excerpt from a movie. It’s a joke about married life.
//he gives the clue for something later CLUE 005
<<if visited_count("TheDeadbitHusband") < 1>>
 <<SetPortrait noportrait>>
 Magnolia: I am sorry!
 <<SetPortrait deadbithusbandportrait1>>
 The Husband: No need! All is well. Too well, you know.
 <<SetPortrait noportrait>>
 Magnolia: (Is he winking?)
 <<SetPortrait deadbithusbandportrait1>>
 The Husband: You seem a little bit down.
 The Husband: Want to hear a joke?
 <<SetPortrait noportrait>>
 Magnolia: Not necessarily...
 <<SetPortrait deadbithusbandportrait1>>
 The Husband: Why didn’t the dad trust the stairs?
 <<SetPortrait noportrait>>
 Magnolia: (I said no, but okay.)
 <<wait 0.5>>
 Magnolia: Uhm, I'm not sure.
 -> Because he was lazy? 
  <<SetPortrait deadbithusbandportrait1>>
  The Husband: Not really creative, are you?
  <<SetPortrait noportrait>>
  Magnolia: (Is this an insult?)
  <<SetPortrait deadbithusbandportrait1>>
  The Husband: He said they were always up to something.
  The Girl: *giggles*
  The Husband: HAHAHAHAHAHAAHA!
  <<SetPortrait noportrait>>
  Magnolia: (I can't even make myself laugh.)
  <<SetPortrait deadbithusbandportrait1>>
  The Husband: You are a tough cookie to crack.
  The Husband: Okay, I have another one.
  <<SetPortrait noportrait>>
   -> Magnolia: Please don't.
   -> Magnolia: Okay, let me hear another one.
    <<jump TheDeadbitHusband_Jokes>>
 -> He was afraid his wife cheats on him upstairs?  
  <<SetPortrait deadbithusbandportrait1>>
  The Husband: HAHAHA! A good one! 
  The Husband: But no.
  The Husband: He said they were always up to something.
  The Girl: *giggles*
  The Husband: HAHAHAHAHAHAAHA!
  <<SetPortrait noportrait>>
  Magnolia: (I can't even make myself laugh.)
  <<SetPortrait deadbithusbandportrait1>>
  The Husband: You are a tough cookie to crack.
  The Husband: Okay, I have another one.
  <<SetPortrait noportrait>>
   -> Magnolia: Please don't.
   -> Magnolia: Okay, let me hear another one.
    <<jump TheDeadbitHusband_Jokes>>
<<else>>
 <<if $hasClue_Graffiti001 is true>>
  <<jump TheHusband_Grafitti>>
 <<else>> 
  <<SetPortrait deadbithusbandportrait1>>
  The Husband: Do you want to hear another one?  
   -> Please no.
   -> Okay.
    <<jump TheDeadbitHusband_Jokes>>
 <<endif>>
<<endif>>
===

title: TheHusband_Grafitti
---
  <<SetPortrait noportrait>>
  Magnolia: I read this graffiti quote from an old movie.
  Magnolia: But I don't remember from which movie it is.
  <<SetPortrait deadbithusbandportrait1>>
  The Husband: Is it one of ours?
  The Husband: If it is, I know all of them by heart.
  <<SetPortrait noportrait>>
  Magnolia: It is.
   -> Tell him the quote.   
    <<SetPortrait noportrait>>
    Magnolia: It was something like...
    Magnolia: [i]We are going to watch the video until the colors melt.[/i]
    <<SetPortrait deadbithusbandportrait1>>
    The Husband: A classic!
    The Husband: It's called [i]The Fall of Rock and Roll[/i]!
    The Husband: It's exactly what I said to my wife on our first night together.
    <<SetPortrait noportrait>>
    Magnolia: (He has a wife??!)
    Magnolia: You have a wife?
    <<SetPortrait deadbithusbandportrait1>>
    The Husband: HAHA! A joke! Good joke.
    <<SetPortrait noportrait>>
    Magnolia: But you said...
    <<SetPortrait deadbithusbandportrait1>>
    The Husband: I mean I do.
    The Husband: But she is not here.
    <<SetPortrait noportrait>>
    Magnolia: What do you mean?
    <<SetPortrait deadbithusbandportrait1>>
    The Husband: She has her purple rabbits, I have my sunny days.
    <<SetPortrait noportrait>>
    Magnolia: (Again with the purple rabbits?!)
    Magnolia: Which means?
    <<SetPortrait deadbithusbandportrait1>>
    The Husband: Everyone has a skeleton in their closet.
    <<SetPortrait noportrait>>
    Magnolia: ...
    <<SetPortrait deadbithusbandportrait1>>
    The Husband: She has her pink glasses on.
    The Husband: That's why the rabbits are pink.
    The Husband: I meant purple.
    The Husband: Purple glasses.
    <<SetPortrait noportrait>>
    Magnolia: And your sunny days?
    <<SetPortrait deadbithusbandportrait1>>
    The Husband: I wear glasses to keep the sun behind them.
    <<wait 1>>
    <<SetPortrait noportrait>>
    Magnolia: Who is this girl then?
    <<wait 1>>
    <<SetPortrait deadbithusbandportrait1>>
    The Husband: Do you not know of the consequences?
    <<set $hasClue_Graffiti001 to false>>
   -> Don't tell him.
   <<SetPortrait noportrait>>
    Magnolia: (I feel too tired to continue this.)
    Magnolia: I have to go.
    Magnolia: See you.
===

title: TheDeadbitHusband_Jokes
---
<<if visited_count("TheDeadbitHusband_Jokes") < 1>>
<<SetPortrait deadbithusbandportrait1>>
The Husband: Okay, okay!
The Husband: Do you know why are elevator jokes so classic and good?
<<SetPortrait noportrait>>
Magnolia: I have no clue.
<<SetPortrait deadbithusbandportrait1>>
The Husband: Come on try a little!
<<SetPortrait noportrait>>
Magnolia: (I think his little girlfriend is going to drop to the floor.)
<<SetPortrait deadbithusbandportrait1>>
The Husband: I'll just tell you.
The Husband: It's because they work on so many levels!
The Husband: HAHAHAHAHAHAH!
The Girl: *faintly gigling*
<<SetPortrait noportrait>>
Magnolia: (She's definitely passing out.)
Magnolia: Hehe.
<<SetPortrait deadbithusbandportrait1>>
The Husband: Yeah it's a good one.
<<elseif visited_count("TheDeadbitHusband_Jokes") == 1>>
<<SetPortrait deadbithusbandportrait1>>
The Husband: This one is really good.
The Husband: What do you call it when a group of apes starts a company?
<<SetPortrait noportrait>>
Magnolia: ...
<<SetPortrait deadbithusbandportrait1>>
The Husband: Monkey business!
The Husband: *laughing furiously*
The Girl: ...
<<SetPortrait noportrait>>
Magnolia: (I can't watch this scene anymore.)
<<elseif visited_count("TheDeadbitHusband_Jokes") == 2>>
<<SetPortrait deadbithusbandportrait1>>
The Husband: Why do some couples go to the gym?
<<SetPortrait noportrait>>
Magnolia: Because they want to be fit?
<<SetPortrait deadbithusbandportrait1>>
The Husband: Probably.
The Husband: But that's not the joke.
<<wait 0.5>>
The Husband: It's because they want their relationship to work out.
The Husband: HAHAHAHAHHA! HAHAH!
<<SetPortrait noportrait>>
Magnolia: (...)
<<elseif visited_count("TheDeadbitHusband_Jokes") == 3>>
<<SetPortrait deadbithusbandportrait1>>
The Husband: You could tell me some jokes.
<<SetPortrait noportrait>>
Magnolia: I am seriously unfunny.
<<SetPortrait deadbithusbandportrait1>>
The Husband: Yeah I can see that.
<<SetPortrait noportrait>>
Magnolia: (...)
<<SetPortrait deadbithusbandportrait1>>
The Husband: Why is Peter Pan always flying?
<<SetPortrait noportrait>>
Magnolia: Why?
<<SetPortrait deadbithusbandportrait1>>
The Husband: Because he Neverlands!
The Husband: Haahahahahahahah!
The Husband: Why are you not laughing!
The Husband: It's so hilarious!
<<SetPortrait noportrait>>
Magnolia: Right.
<<else>>
<<SetPortrait deadbithusbandportrait1>>
The Husband: Actually, I have no more jokes to tell you.
<<SetPortrait noportrait>>
Magnolia: (I am thankful for that.)
Magnolia: That's okay.
<<endif>>
===

title: TheAllKnowingLady
---
//this time she is the trigger, it's hidden in her dialogue TRIGGER 002
<<if $talkedaboutrabbits is true>>
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: You have nowhere else to go.
 The Knowing Lady: This is what you searched for, isn't it?
 <<SetPortrait noportrait>>
  -> Hold her hand.
  -> Nope.
<<else>>
 <<if visited_count("TheAllKnowingLady") < 1>>
 <<SetPortrait noportrait>>
 Magnolia: (I would never do something like this before.)
 Magnolia: (I would just probably pass by this lady hoping she wouldn't ask to read my palm.)
 Magnolia: (But I need to find a way back.)
 Magnolia: Excuse me?
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: Yes birdie? 
 <<SetPortrait noportrait>>
 Magnolia: Have you seen anything strange around here?
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: It depends on what you consider strange.
 The Knowing Lady: For me, there is nothing strange.
 The Knowing Lady: I know everything even before it happens.
 <<SetPortrait noportrait>>
 Magnolia: (Exactly the person I need.)
 <<jump TheAllKnowingLady_Questions>>
 <<else>>
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: I could give you an answer to any question.
 The Knowing Lady: But I am careful.
 <<jump TheAllKnowingLady_Questions>>
 <<endif>>
<<endif>>
===

title: TheAllKnowingLady_Questions
---
-> Somebody told me parents shouldn't die before their children. <<if $hasClue_AllKnowingLady is true>>
 <<set $talkedaboutrabbits to true>>
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: That is true.
 The Knowing Lady: But they still do.
 The Knowing Lady: The realm of souls doesn't know of the same order we know.
 <<SetPortrait noportrait>>
 Magnolia: The same person told me a child is no longer if their parents die.
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: That bird must be caged inside their own life.
 The Knowing Lady: They would know that kind of love engraves itself in the texture of the world.
 The Knowing Lady: You can't wash it off.
 The Knowing Lady: But why do you worry about that?
 The Knowing Lady: You have no parents.
 <<SetPortrait noportrait>>
 Magnolia: That's why I worry. They died.
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: You believe they died?
 <<SetPortrait noportrait>>
 Magnolia: I'm afraid I don't believe in afterlife.
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: That's not what I was talking about.
 <<SetPortrait noportrait>>
 Magnolia: I don't think I understand you.
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: I'll just tell you...
 The Knowing Lady: Never follow the purple rabbits!
 <<SetPortrait noportrait>>
 Magnolia: I've never even seen a purple rabbit!
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: You will.
 The Knowing Lady: Hold my hand.
 <<SetPortrait noportrait>>
  -> Hold her hand.
  <<set $trigger_TheAllKnowingLady to true>>
  -> I am too afraid.
-> Do you know of a way to enter dreams?
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: Nobody dreams the same.
 The Knowing Lady: Some people are so obsessed with mundane things in their daily lives...
 The Knowing Lady: They enter only the surface layer of the land of dreams.
 <<SetPortrait noportrait>>
 Magnolia: So there is a land of dreams?
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: Some believe we are dreaming even now.
 The Knowing Lady: Who is to say there isn't?
 <<SetPortrait noportrait>>
 Magnolia: (She didn't answer me.)
-> Do you believe a person could be resurrected?
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: You shouldn't ask questions you are not ready for.
 <<SetPortrait noportrait>>
 Magnolia: So that means it's a no?
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: I didn't say that.
 <<SetPortrait noportrait>>
 Magnolia: What did you mean then?
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: Avoid the looped paths.
 <<SetPortrait noportrait>>
 Magnolia: You are not going to tell me anything more?
 <<SetPortrait allknowingladyportrait1>>
 The Knowing Lady: Ask when you are ready.
===

title: TheAntiqueShop
---
<<SetPortrait noportrait>>
//this is where you get the object OBJECT d003
Magnolia: (This seems like a place you could find magical stuff in.)
Magnolia: (I played this one game.)
Magnolia: (The shop where i bought a flaming fox looked exactly like this.)
 -> Enter it.
   <<set $entertheantique to true>>
 -> No.
===

//INSIDE THE ANTIQUE SHOP

title: TheObject_Antique
---
<<SetPortrait noportrait>>
Magnolia: (I don't have any money.)
 -> Steal it.
  <<set $putintheinventory003 to true>>
 -> I can't do it.
=== 

title: AnOldEnvelope
---
<<SetPortrait noportrait>>
-> Read the envelope.
 The Envelope: I mistake the rain for your tears.
 The Envelope: And on those days just before it rains, I feel like you are close.
 <<set $haspoetclue to true>>
===

title: TheAntiqueDoor
---
<<SetPortrait noportrait>>
-> Exit the shop.
 <<set $exittheantique to true>>
-> Stay.
===

//ADDED NODES

title: TheCrash02
---
<<SetPortrait noportrait>>
<<if visited_count("TheCrash02") < 1>>
Magnolia: (Oh so I am on the other side of that crash now.)
Magnolia: (It's like I am sleepwalking to the other parts of the city.)
<<else>>
Magnolia: (Can I use my dreams to travel?)
<<endif>>
===

title: TheHedgeDoor
---
<<SetPortrait noportrait>>
Magnolia: (This door has the exact texture of that hedge.)
===

title: TheFog01
---
<<SetPortrait noportrait>>
<<if visited_count("TheFog01") < 1>>
Magnolia: (The fog is too thick.)
Magnolia: (I might have something to lose at the end...)
Magnolia: (My life.)
Magnolia: (I'm not ready for that.)
<<else>>
Magnolia: (I was always scared of the dark.)
<<endif>>
===

title: TheHoop
---
<<SetPortrait noportrait>>
Magnolia: (I hear people like team games.)
Magnolia: (I on the other hand always avoid them.)
===

title: TheMountains
---
<<SetPortrait noportrait>>
Magnolia: (These mountains look so wild.)
Magnolia: (They are towering over the city.)
===

title: TheWeirdFog
---
<<SetPortrait noportrait>>
Magnolia: (This looks out of place.)
Magnoli: (What if it's one of the ways to fall into a dream?)
 -> Touch the suspended particles.
  <<wait 1>>
  Magnolia: (Nothing is happening.)
  Magnolia: (This is embarrassing.)
 -> Ignore it.
===

title: TheFountain
---
<<SetPortrait noportrait>>
Magnolia: (Those people that drop coins to make wishes are fools.)
<<wait 0.5>>
Magnolia: (But I am the one chasing a dream.)
 -> Look for a coin.
  Magnolia: (I should check my pockets.)
===

title: DroppedTheCoin
---
<<set $coinisselected to true>>
<<SetPortrait noportrait>>
//when the player picks the coin from the inventory
<<wait 1>>
Magnolia: (I wish this all turns out to be true.)
<<wait 0.5>>
Magnolia: (If not, I wish to never be sane again.)
===

title: TheUphillSee
---
<<SetPortrait noportrait>>
Magnolia: (Oh, I can see my aunt's neighbourgood from here.)
<<wait 0.5>>
Magnolia: (Is that the car guy from before?)
===

title: TheSunDrawing
---
<<SetPortrait noportrait>>
Magnolia: (This is not the first time that I see sun drawn around.)
Magnolia: (Are they all just pretending there is sun, but secretly missing the sunny days?)
===

title: TheManHole02
---
<<SetPortrait noportrait>>
<<if visited_count("TheManHole02") < 1>>
Magnolia: (Another one.)
Magnolia: (There is a word here too.)
 -> Read it.
  The Word: THIEF
  <<wait 0.2>>
  Magnolia: (The thief?)
<<else>>
 Magnolia: (The thief.)
 Magnolia: (Who says there is even a connection there.)
 Magnolia: (I'm looking too much into it.)
<<endif>>
===