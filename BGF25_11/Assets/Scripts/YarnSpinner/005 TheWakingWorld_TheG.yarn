//NOW WE ARE IN THE ZONE V THE G

title: TheG
---
//for UNITY
<<set $putintheinventory004 to false>> //old comb at grandpas house that has little stones which look like eyes (Used in 4th DREAMWORLD)
<<set $putintheinventory005 to false>> //object we find on the roof, it's some kind of a machine part (used in the 6th DREAMWORLD)
<<set $goontheroof to false>> //moves the player to the roofs, same map
<<set $lookaround to false>> //triggers a CUT SCENE to look around the city
<<set $entertheschool01 to false>> //this moves the character behind the school on the map and puts an item in the inventory
<<set $entertheschool02 to false>>//this just moves the character
<<set $enterthegranshouse to false>> // enter the grandpas house
<<set $godowntheroof to false>> // go down the roof
<<set $gototheMuseum to false>> //this loads the next scene, it is in front of the bridge
<<set $playscreamsound to false>> //plays a sound of Magnolia shouting on top of her lungs
<<set $sheisdancing to false>> //plays ANIMATION of a THe One To Impress dancing
<<set $pulsingeffect to false>> //sound and visual effect of fear, pulsing
<<set $fallingfeeling to false>> //again some kind of woozy effect

//for YARN
<<set $gavethebook01 to false>>
<<set $gavethebook02 to false>>
<<set $askedthisquestion to false>>
<<set $hasclue_fly to false>>
<<set $hascomb to false>>

Magnolia: (This is getting out of hand.)
Magnolia: (I'm starting to believe it.)
Magnolia: (At the same time I feel like I'm losing contact with reality.)
Magnolia: (I am afraid of what I am capable of under these circumstances.)
<<wait 0.5>>
Magnolia: (Also, I still don't know where that damn star is.)
Magnolia: (I don't even know if I should search here or in the dreams.)
===

title: TheGuilty
---
<<if visited_count("TheGuilty") < 1>>
 The Guilty: God forgives your sins, but society never does.
 Magnolia: (There has been a lot of God talk lately.)
 The Guilty: Sun even seems to be judging me.
 The Guilty: Back home sun couldn't reach at least.
 Magnolia: And where is home?
 The Guilty: They put me there to punish me.
 The Guilty: But they saved me from judgment.
 Magnolia: You went to prison?!
 The Guilty: You can call it that.
 Magnolia: How do you call it?
 The Guilty: It was my second chance.
 Magnolia: But I thought your second chance is here.
 The Guilty: There are no chances in this world.
 <<wait 0.5>>
 Magnolia: (I don't know if this guy can help me.)
  <<jump TheGuilty_Questions>>
<<else>>
 The Guilty: Do you know a way for me to go back?
  <<jump TheGuilty_Questions>>
<<endif>>
===

title: TheGuilty_Questions
---
-> What was the prison like?
 The Guilty: The sun didn't touch us.
 Magnolia: Why are you afraid of the sun?
 The Guilty: I am not afraid. 
 The Guilty: But it burns.
 <<wait 0.2>>
 The Guilty: In prison, you don't have that many options.
 The Guilty: That's the only place where I believe in destiny.
 The Guilty: Everything was predetermined.
 Magnolia: But why is that good?
 Magnolia: Don't you want the freedom to choose?
 The Guilty: Freedom is a burden. I never make the right choice.
 Magnolia: You could learn how to make the right choice.
 The Guilty: How do you know something is the right choice?
 Magnolia: For some things you just know.
 Magnolia: That's why there are laws, morals, and personal beliefs.
 The Guilty: Moral codes and beliefs are quite subjective. That's why I turn to the law.
 The Guilty: However, I am not good at studying, so I prefer to stay close as possible to it.
 Magnolia: I don't think I would like that.
 The Guilty: It's not for everyone.
 <<wait 0.5>>
 The Guilty: Did you ever commit a crime?
 <<set $pulsingeffect to true>>
 <<wait 0.5>>
 Magnolia: I don't think I have.
 Magnolia: But I don't remember much.
 The Guilty: Maybe you are trying to forget something you did.
 Magnolia: It's...It's not that.
 Magnolia: It was an accident.
 The Guilty: You caused an accident?
 Magnolia: No!
 Magnolia: I was not there.
 The Guilty: I'll believe you.
 Magnolia: But it's the truth.
 <<wait 0.5>>
 Magnolia: I have to go.
-> How long did you stay there?
 The Guilty: I am not sure.
 The Guilty: My original sentence was 30 years, but they let me go after just a few.
 Magnolia: (30 years?! What could have he possibly done?!)
 Magnolia: Why did they let you out?
 The Guilty: For the exceptional behavior, unfortunately!
 The Guilty: But I just really liked it there.
 <<jump TheGuilty_Questions>>
-> Do you have any friends inside?
 The Guilty: Not anymore.
 Magnolia: They got out?
 The Guilty: They killed them all just before I left.
 Magnolia: They can kill people?!
 The Guilty: They were not people.
 The Guilty: But what does that matter?! Do their deaths sadden you less now?
 Magnolia: No, I...Of course not.
 Magnolia: What were they?
 <<wait 0.5>>
 The Guilty: They would come to my room and sleep under my feet.
 The Guilty: The music of their nocturnal lives put me to sleep.
 The Guilty: We could never understand each other through language, but we knew well our thoughts.
 The Guilty: We were all free in that cage.
 Magnolia: (It's not again rabbits is it?)
 <<jump TheGuilty_Questions>>
-> Do you know how I could get to the train station? <<if $askaboutstation is true>>
 The Guilty: Are you sure you want to get there?
 Magnolia: Pretty sure.
 The Guilty: You could try to go through the park.
 <<if visited_count("ThePark") > 0>>
  Magnolia: I tried. I can't get in without a rabbit.
  The Guilty: Ah, that's true.
  The Guilty: Maybe you could fly over it?
  <<set $hasclue_fly to true>>
  Magnolia: Are you being serious?
  The Guilty: I might be.
 <<else>>
  Magnolia: Oh so that's the way.
 <<endif>>
-> Never mind.
===

title: TheChild
---
<<if visited_count("TheChild") < 1>>
 Magnolia: (I don't see his parents around.)
 Magnolia: (Even tho nothing seems to make sense anymore, I'm still worried about the little ones.)
 <<wait 0.2>>
 Magnolia: Hey kid, are you lost?
 The Child: I know my way home.
 Magnolia: That's good.
 Magnolia: Do you want me to walk you there?
 The Child: I can't talk to strangers.
 Magnolia: I understand that.
 Magnolia: Do you at least live close by?
 The Child: Yes.
 The Child: But my home changed.
 Magnolia: How did it change?
 The Child: Some strange people are living inside.
 Magnolia: (?)
 Magnolia: So you can't find your parents?
 The Child: I know where my parents live.
 The Child: They are just not there.
  <<jump TheChild_Questions>>
<<elseif visited_count("TheChild") == 1>>
 Magnolia: (This situation seems too serious for me to handle it.)
 Magnolia: (Does this child have some kind of memory loss, or delusions?)
<<else>>
 Magnolia: (I still should try to help, right?)
<<endif>>
===

title: TheChild_Questions
---
-> What do you mean they are not there?
 The Child: That's where I left them.
 The Child: But when I came back, it was some other people living inside.
 Magnolia: Some strangers are living inside your house?
 The Child: Yes.
 Magnolia: Are you sure you got the right house?
 The Child: It's my house. I know my house.
 Magnolia: Okay, but why would your parents leave?
 The Child: They left?!
 Magnolia: No, I don't know. 
 Magnolia: I'm sure they are somewhere close by, looking for you.
 The Child: I don't think they are looking for me.
 Magnolia: Why do you think like that?
 The Child: They would find me. 
 The Child: I always play at the same spot.
 The Child: This is where the frogs come out.
-> When did you see your parents last?
 The Child: In the morning when they left for work.
 Magnolia: Do you know where they work?
 The Child: It's somewhere important.
 The Child: They never let me bother them when they work.
 Magnolia: But do you know what your parents do?
 The Child: No. They never told me.
 The Child: I don't like when they work.
 Magnolia: Why?
 The Child: Their eyes disappear.
 Magnolia: Their eyes?
 The Child: Then they can't look at me anymore.
 The Child: I never saw their eyes when they talk about work.
 The Child: It scares me.
 Magnolia: It's okay. I'll try to make it right.
 <<jump THeChild_Questions>>
-> Do you want me to take you to the police to help you find your parents?
 The Child: I don't want them to arrest my parents.
 The Child: Then I'll see them even less.
 Magnolia: They are not going to arrest them.
 Magnolia: (I think. They did leave a child alone.)
 Magnolia: They will just help you find them again.
 Magnolia: They will ask those strangers in your house who they are.
 The Child: I asked.
 Magnolia: You asked?
 Magnolia: What did they say?
 The Child: They told me I look like one of their children.
 Magnolia: And nothing else?
 The Child: No.
 Magnolia: That's strange.
 <<jump TheChild_Questions>>
-> (It's not for me to deal with this.)
===

title: TheKindGrandpa01
---
//he talks nonsense, but he invites you in for a tea
//you get him to talk about the roof
//you go out on the roof
//you get an dOBJECT 004
The Kind Grandpa: You seem like such a sunny creature.
Magnolia: Thank you?
The Kind Grandpa: Three little purple rabbits came this morning for their milk.
Magnolia: Did you say purple rabbits?
The Kind Grandpa: Kittens, yes. They were so lovely.
The Kind Grandpa: I put the bowl under the table, so it doesn't go bad under the sun.
Magnolia: There was no sun this morning.
The Kind Grandpa: The sun is my only friend these days.
The Kind Grandpa: Old age is lonely.
The Kind Grandpa: Do you maybe want a cup of tea?
 -> That would be nice.
  <<set $enterthegranshouse to true>>
 -> No? 
===

title: TheGrandpaHouse
---
Magnolia: (This smell is the smell of old books.)
Magnolia: (But this doesn't bring back any memories.)
Magnolia: (All my grandparents died long ago.)
<<wait 1>>
The Kind Grandpa: Feel at home, my dear.
===

title: TheGrandpaObject
---
Magnolia: (Why do I feel like the little dots on this old comb are eyes?)
Magnolia: (The last time that **** completely transformed in my dream.)
Magnolia: (And this comb already seems kind of mystical.)
Magnolia: (But I shouldn't steal from the old.)
 -> Take it anyway.
   <<set $putintheinventory004 to true>>
   <<set $hascomb to true>>
   The Kind Grandpa: It was given to my wife by her first love.
   The Kind Grandpa: She was seventeen.
   <<wait 0.5>>
   Magnolia: (Did I get caught?)
   Magnolia: (Should I return it?)
    -> Yes.
     Magnolia: (I feel no guilt anymore.)
     Magnolia: (I have to see this through to the end.)
    -> Keep it.
     Magnolia: (It's okay to be selfish.)
  <<wait 0.5>>
   Magnolia: Didn't it bother you?
   Magnolia: That she kept it?
   The Kind Grandpa: Oh, never.
   The Kind Grandpa: I found it always very romantic.
   The Kind Grandpa: I very well understand a person who would want to give her such a nice gift.
   Magnolia: That seems like a healthy way to think.
   The Kind Grandpa: It's just very natural to me.
===

title: TheGrandpa_Inside
---
<<if visited_count("TheGrandpa_Inside") < 1>>
 Magnolia: (He seems so fragile.)
 Magnolia: (Old people always saddened me.)
 Magnolia: (It always felt like all those young people stuck in old people's bodies.)
 Magnolia: (I wonder if he feels like that.)
 Magnolia: (But I don't want to bother him for now.)
<<else>>
 Magnolia: (Let him cook his tea.)
<<endif>>
===
title: GrandpasDoor
---
Magnolia: (I don't know how to open this thing.)
Magnolia: (I could ask him.)
===

title: TheVase
---
//granpa talks about his life
Magnolia: This vase seems truly old.
The Kind Grandpa: It's older than me.
The Kind Grandpa: However, it's not that important.
The Kind Grandpa: Nor valuable.
The Kind Grandpa: It's a fake.
Magnolia: (If something is fake, it's not worth it.)
Magnolia: (Even if it looks the same.)
Magnolia: (Feels the same.)
<<wait 0.5>>
The Kind Grandpa: However, its purpose was once really important.
Magnolia: It was?
The Kind Grandpa: You see that door behind?
Magnolia: Not really?
The Kind Grandpda: Exactly.
The Kind Grandpa: The grandiose presence of that fake vase is hiding the existence of a door.
The Kind Grandpa: And that door was used for escaping in dire situations.
Magnolia: What kind of situations?
The Kind Grandpa: It's not polite to talk of depressing matters under the sun.
Magnolia: But it's not sunny...
The Kind Grandpa: The tea is almost ready.
Magnolia: (Now everything sounds like a clue on a quest.)
Magnolia: (I can't help to think I should go on that roof.)
 -> Stop listening and go on the roof.
  Magnolia: (Maybe he will forgive me.)
  <<set $goontheroof to true>>
 -> Listen to him.
  Magnolia: (I can't d that anymore.)
  Magnolia: (I'm too distracted.)
   -> Go on the roof.
   <<set $goontheroof to true>>
===

title: TheRoofObject
---
//you find the object on the roof OBJECT 005
Magnolia: (I don't know what I'll use this for.)
 -> Pick it up anyway.
  <<set $putintheinventory005 to true>>
 -> Leave it.
===

title: TheSpot
---
//from here you see the station
Magnolia: (I have never seen the city from this height.)
-> Look at the distance.
 <<set $lookaround to true>>
 //there should be a CUTSCENE with Magnolia seeing train station in the distance
-> Let out a scream.
 <<set $playscreamsound to true>>
 Someone: The Gods are calling us!
 Magnolia: (I am not a God.)
//you get a clue
===

title: TheRoofEnd
---
Magnolia: (These are stairs to go down.)
Magnolia: (They look safe enough.)
 -> Go down.
  <<set $godowntheroof to true>>
 -> Stay.
===

title: TheOneToImpress
---
<<if visited_count("TheOneToImpress") < 1>>
 Magnolia: (She is pretty.)
 The One To Impress: Don't be shy. 
 The One To Impress: How do I look?
 Magnolia: I absolutely love your dress.
 The One: Only my dress?
 Magnolia: (I don't know what to tell her.)
 Magnolia: I can't see your face.
 The One: I have no face.
 Magnolia: What do you mean?
 The One: I made a mistake.
 Magnolia: What kind of a mistake?
 The One: I dared to live how I wanted.
 Magnolia: That's how you lose a face?
 The One: I should have listened.
 <<wait 0.5>>
 The One: But I'm still beautiful.
 Magnolia: You are.
 The One: Do you want to see me dance?
  -> I would love to.
   <<set $sheisdancing to true>>
  -> I don't have time.
  The One: Goodbye. 
//she is there just for the talk, maybe we get some clue before to unlock more parts of the conversation
<<elseif visited_count("TheOneToImpress") == 1>>
 The One: Until I am pretty I live.
 Magnolia: What happens after?
 The One: The sun will burn my skin and turn me into dust.
 Magnolia: Then you will still exist in the wind.
 The One: Exist, but not live.
 The One: It's almost the same as now.
 Magnolia: I think I might understand what you are telling.
<<else>>
 Magnolia: (She makes me sad.)
 Magnolia: (I'll just let her dance.)
<<endif>>
===

title: TheSecurity
---
//gives you access through the school, so you could get to the bridge
//you need to give him the grief book to read for him to let you
//there will be another way to pass, but only if you give him a book, you get another object while passing
<<if visited_count("TheSchool") > 0>>
 <<if $askedthisquestion is true>>
  Magnolia: (I still can't get what he said out of my head.)
   -> Do you know any children that died?
    <<jump TheSecurity_Children>>
 <<else>>
  The Security: What makes you come here on a break day miss?
  Magnolia: (I didn't know it was a break.)
  Magnolia: I am looking for a way to go to that bridge.
  The Security: Without kids, the school has no reason to be open.
  Magnolia: But do you have keys? 
  Magnolia: I just need to pass.
  The Security: You should find the other way.
  Magnolia: But there seems to be no other way!
  The Security: There is always another way.
  The Security: Maybe you should come back when you are a child.
  Magnolia: When I am a child?
  The Security: Time started going backward.
  Magnolia: Since when?
  The Security: With each child that dies, time goes a little bit backward.
  Magnolia: (It's a bit creepy?)
  -> Do you know any children that died?
   <<jump TheSecurity_Children>>
  -> What if I can't come back as a child?
   The Security: That's your problem miss.
   Magnolia: But I want to go to the other part of the city!
   The Security: That's too bad.
   <<set $askedthisquestion to true>>
 <<endif>>
<<else>>
 <<if visited_count("TheSecurity") < 1>>
  The Security: What makes you come here on a break day miss?
  Magnolia: I'm not sure.
 <<else>>
  Magnolia: (I don't need to disturb him.)
 <<endif>>
<<endif>>
===

title: TheSecurity_Children
---
   The Security: My child died.
   Magnolia: Really?
   Magnolia: I'm so sorry.
   The Security: No need, the time will pick him up and bring him back.
   Magnolia: So you think time resurrects?
   The Security: I don't know about any of that.
   The Security: I just know I'll see him again.
   Magnolia: That's nice.
   Magnolia: (Maybe he has a bigger need for that grief book than me.)
   Magnolia: (I also found that old fiction book about tree roots.)
   Magnolia: (Maybe anything that would distract him is good.)
    -> Give him your book.
      <<set $gavethebook01 to true>>
      Magnolia: (I need to fetch it from the backpack.) //player needs to open the inventory
    -> Give him the other book.
       <<set $gavethebook02 to true>>
       Magnolia: (I need to fetch it from the backpack.)
===

title: TheSchool
---
Magnolia: (This seems like the only way to get to the bridge.)
<<wait 1>>
Magnolia: (I barely remember my school days.)
Magnolia: (It feels so distant.)
<<if $gavethebook01 is true>>
 -> Don't go in.
 -> Go in.
  <<set $entertheschool01 to true>> //this just moves the character to another place behind the school
<<elseif $gavethebook02 is true>>
 -> Don't go in.
 -> Go in.
  <<set $entertheschool02 to true>> //this just moves the character to another place behind the school
<<else>>
 Magnolia: (I don't have the key to the school.)
 Magnolia: (The security guard might have it.)
<<endif>>
===

title: TheBridge
---
Magnolia: (I have never been to this part of the city.)
-> (I still want to check out something here.)
-> Pass the bridge.
 <<set $gototheMuseum to true>>
===

//ADDED NODES

title: TheCrashScene
---
Magnolia: (The world is falling into chaos.)
Magnolia: (I can't go there.)
===

title: TheButterfly
---
<<if visited_count("TheButterfly") < 1>>
Magnolia: (This is where my parents stood in that dream.)
Magnolia: (For a second I got my memories back.)
Magnolia: (But now everything is blurry again.)
<<wait 0.5>>
Magnolia: (I wonder who drew this butterfly.)
<<else>>
Magnolia: (I just want to sit and draw butterflis.)
<<endif>>
===

title: TheRoofWindow
---
<<if visited_count("TheRoofWindow") < 1>>
A male voice: Who is up for an amusement park day tomorrow.
Kids: Me! Me! Mememememe! Nooo, me!
A male voice: Hey! Don't fight. We'll all go.
Kids: Yaaaay! Thank youu!
<<wait 0.5>>
A male voice: Don't forget to bring your astrounaut suits.
<<wait 0.5>>
Magnolia: (Astrounut suit?!)
Kids: What about our wishes?
A male voice: Pack them too, you might be able to use them tomorrow.
Magnolia: (What does that mean?!)
<<else>>
Magnolia: (They are awfully quiet now.)
Magnlia: (Are they also looking for the star?)
<<endif>>
===

title: ADate
---
Magnolia: (I've never been on a date.)
<<wait 0.5>>
Magnolia: (Have I ever fallen in love?)
Magnolia: (What if I had a partner and forgot about them after the accident?)
<<wait 0.5>>
Magnolia: (Haha, no way.)
===

title: AFall
---
<<set $fallingfeeling to true>>
<<wait 0.5>>
Magnolia: (If I fall now and die...)
Magnolia: (Will I wake up and realize it was all a messed up dream?)
//destroy it after
===

title: TheWilderness06
---
Magnolia: (It's like mountains are getting taller.)
Magnlia: (And the forest is closing in.)
===

title: TheOtherEnd
---
Magnolia: (The road tehre is falling apart.)
Magnolia: (I'm not in a dream, I can't risk my life.)
===

title: KidsDrawings
---
Magnolia: (Again a sun.)
Magnolia: (If anything, the clouds are getting darker.)
<<wait 0.5>>
Magnolia: (I just need to move forward.)
Magnolia: (Somehow I'll stumble upon that star.)
===