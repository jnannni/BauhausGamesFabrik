//WE ARE NOW IN THE ZONE 02 THE BEACH

title: TheBeach
---
//variables for UNITY
<<set $goverthebridge to false>> //brings us to the last level Amusement park(but I would think of maybe putting it on the same level and having the camera follow Magnolia and Chihiro walk over the bridge and to the Island)
<<set $cutscene_moon to false>> //triggers a CUTSCENE (magnolia watches the water and the reflection of the moon)
<<set $mournersmove to false>> // triggers in-game animation of mourners moving to show a pattern in the sand
<<set $magnoliainarow to false>> // in-game animation of Magnolia walking to the Mourners and stands in the same row (she has some thoughts, but nothing happens)
<<set $putintheinventory12 to false>> //puts the daisies in the inventory
<<set $everythingchanges to false>> //enviromental changes
<<set $activated to false>>
<<set $fadechihiro to false>>

//variables for in YARN
<<set $hasclue_flowers to false>>
<<set $everythingchanged to false>>

Magnolia: (I have never been to this beach before.)
Magnolia: (I'm sure I would have remembered.)
<<wait 0.5>>
Magnolia: (Maybe my parents didn't think I need to see the beach.)
<<wait 0.5>>
Magnolia: (It's not like I care.)
Magnolia: (There's nobody to take me now.)
//new objective: cross the bridge
===

title: TheBookReader
---
//she can't get out of the world of the book
//she believes it’s true, and that when magnolia talks to her she is dreaming
<<if visited_count("TheBridge02") < 1>>
 Magnolia: (She's probably reading about the purple rabbits.)
 Magnolia: (As I said, I don't have time.)
 Magnolia: (I need to cross that bridge.)
<<else>>
 <<if visited_count("TheBookReader") < 1>>
  Magnolia: (Maybe they know something.)
  Magnolia: Hey, sorry to bother you.
  <<wait 0.5>>
  The Reader: ...
  Magnolia: Hey?
  The Reader: Oh?
  The Reader: Sorry.
  Magnolia: Do you maybe know any other way to go to that amusement park?
  The Reader: Which amusement park?
  Magnolia: That one on the island.
  The Reader: Sorry I can't help. I'm here for the first time.
  The Reader: My dreams are rarely at the same location.
  Magnolia: Your dreams?
  The Reader: Yes. We are in my dream right now.
  Magnolia: Are we?
  The Reader: Yes.
  <<wait 0.5>>
  Magnolia: (I don't know what she thinks by that.)
  <<wait 0.5>>
  Magnolia: What are you reading?
  The Reader: Reading?
  Magnolia: Yeah, you have a book in your hand.
  The Reader: Oh that?
  Magnolia: That's probably just my diary.
  <<jump TheBookReader_Question>>
 <<else>>
  Magnolia: (I don't think she can help me.)
  Magnolia: (I can still try.)
  <<jump TheBookReader_Question>>
 <<endif>>
<<endif>>
===

title: TheBookReader_Question
---
-> You don't have a clue how to get there?
 The Reader: I'm sorry, I don't.
 Magnolia: That's okay.
-> What is your life like?
 The Reader: I am a warrior.
 Magnolia: What kind of warrior?
 The Reader: I don't work for anyone, and I wander around.
 The Reader: Right now I am probably sleeping somewhere by the road.
 The Reader: I do that often on my voyages.
 <<wait 0.5>>
 Magnolia: And whom do you fight?
 The Reader: I fight the thieves. They steal from the poor, despite them being rich themselves.
 Magnolia: So it's something like Robin Hood?
 The Reader: I don't know this Robin Hood?
 The Reader: Is he famous?
 Magnolia: Kind of.
 Magnolia: (Should I feed their delusions?)
 <<wait 0.5>>
 Magnolia: I'm sure you're better than him anyway.
 The Reader: It's not about competition.
 The Reader: I welcome any ally.
-> Can I see your diary?
 The Reader: Yes, why not? Here, take it.
  -> Take the diary.
   Magnolia: (Interesting.)
   Magnolia: (It looks just like a normal fictional book.)
   Magnolia: (It even has a publisher.)
   <<wait 0.5>>
   Magnolia: (I think she believes her real life is inside this book.)
   Magnolia: (Maybe it's better that way.)
-> Nothing.
===
title: TheMourners
---
<<if visited_count("TheBridge02") < 1>>
 Magnolia: (They seem like a happy bunch.)
 <<wait 0.5>>
 Magnolia: (I can't waste my time with that.)
<<else>>
 <<if visited_count("TheBookReader") < 1>>
  Magnolia: (They look like they are going to a funeral.)
  Magnolia: (They are terribly serious.)
  <<wait 0.5>>
  Magnolia: (Or are they here to finally tell me that I escaped a mental hospital, and all of this was just a bad trip?)
  Magnolia: (I think I would honestly feel relief if that's the case.)
  <<jump TheMourners_Options>>
 <<else>>
  Magnolia: (I don't know what I am back here.)
  <<jump TheMourners_Options>>
 <<endif>>
<<endif>>
===
title: TheMourners_Options
---
   -> Stand with them.
    <<set $magnoliainarow to true>>
   -> Give them flowers. <<if $hasclue_flowers is true>>
    <<set $mournersmove to true>>
    <<wait 1>>
    <<jump TheMourners_Next>>
   -> (They scare me.)
===
title: TheMournesrs_Next
---
 Magnolia: (The moon casts the shadows of the mourners curiously.)
 Magnolia: (It makes it look like streams of water that flow into the sea.)
 <<wait 0.5>>
 <<set $everythingchanges to true>>
  Magnolia: (What does this mean?)
  Magnolia: (Why does this look like from my dream?)
  Magnolia: (Is everything just collapsing?)
  <<wait 1>>
   <<set $everythingchanged to true>>
===

title: TheWildFlowers
---
//give the wildflowers to the mourners, they move in a way that lets the rays of the moon fall onto the beach in a certain way, showing a certain pattern
//Magnolia needs to dig this pattern, and as soon as she does, there are waterfalls and the water is red, and the dream is slowly sipping into the wakingworld
//only after this chihirio is available to talk to
<<if visited_count("TheBridge02") < 1>>
 Magnolia: (I used to like flowers.)
<<else>>
 <<if visited_count("TheBookReader") < 1>>
  Magnolia: (I used to like flowers.)
  <<wait 0.5>>
  Magnolia: (These are daisies.)
  Magnolia: (The gardener's wife’s favorite.)
   -> Pick them up.
    <<set $putintheinventory12 to true>>
    <<set $hasclue_flowers to true>>
   -> Leave them be.
 <<else>>
  Magnolia: (I used to like flowers.)
  <<wait 0.5>>
  Magnolia: (These are daisies.)
  Magnolia: (The gardener's wife’s favorite.)
   -> Pick them up.
    <<set $putintheinventory12 to true>>
    <<set $hasclue_flowers to true>>
 <<endif>>
<<endif>>
===

title: TheMoonReflection
---
//she is the eclipse of the moon, she is the sun
<<if visited_count("TheBridge02") < 1>>
 Magnolia: (That astronomer was afraid of the moon.)
 Magnolia: (I can see why.)
<<else>>
 <<if visited_count("TheBookReader") < 1>>
  Magnolia: (The astronomer was afraid of the moon.)
  <<wait 0.5>>
  Magnolia: (I can see why.)
  Magnolia: (The sea is obviously in love with it.)
  <<jump TheMoon_Options>>
 <<else>>
  Magnolia: (I understand the sea.)
  Magnolia: (I feel pulled by it.)
  <<jump TheMoon_Options>>
 <<endif>>
<<endif>>
===

title: TheMoon_Options
---
-> Sit and watch the reflection.
 <<set $cutscene_moon to true>>
 Magnolia: (There seems to be a shadow over the moon.)
 Magnolia: (It doesn't look like a normal eclipse.)
 <<wait 0.5>>
 Magnolia: (Why does that shadow look like me?)
 <<wait 0.5>>
 Magnolia: (Why is it that wherever I look I see myself?)
-> I don't have the time.
 Magnolia: (When this is all over, I'll sit and watch the moon.)
===

title: ALostDiary
---
//this is just a story within a story
<<if visited_count("TheBridge02") < 1>>
 Magnolia: (If it's not a magic star I don't care.)
 Magnolia: (Somebody else will pick it up.)
 <<wait 0.5>>
 Magnolia: (Where is that bridge?)
<<else>>
 <<if visited_count("TheBookReader") < 1>>
 <<else>>
 <<endif>>
<<endif>>
===

title: TheSunBather
---
<<if visited_count("TheBridge02") < 1>>
 Magnolia: (Must be nice pretending that there is the sun.)
<<else>>
 <<if visited_count("TheBookReader") < 1>>
  Magnolia: (Must be nice pretending that there is the sun.)
  <<wait 0.5>>
  Magnolia: (Is that a sunburn on their back?)
  Magnolia: (Well it's not weirder than that ray of sun in the factory.)
 <<else>>
  Magnolia: (Must be nice pretending that there is the sun.)
 <<endif>>
<<endif>>
===

title: Chihiro
---
  Chihiro: Hey.
  Magnolia: Hey.
  Chihiro: My name is Chihiro.
  Magnolia: My name is Magnolia.
  <<wait 0.5>>
  Chihiro: There's no reason to keep you away anymore.
  Magnolia: From where?
  Chihiro: From the star.
  Magnolia: You know about the star?
  Chihiro: Hold my hand.
   -> Hold Chihiro's hand.
    <<set $fadechihiro to true>>
    <<wait 1.7>>
    <<set $goverthebridge to true>>
    <<set $activated to true>>
===

title: TheBridge02
---
<<if visited_count("TheBridge02") < 1>>
 Magnolia: (This is it.)
 Magnolia: (I can't believe it's almost over.)
 Magnolia: (I just need to cross this bridge.)
 <<wait 1>>
 Magnolia: (No, please no.)
 <<wait 0.5>>
 Magnolia: (How can anyone even cross this bridge?!)
 Magnolia: (Half of the bridge is missing!)
 <<wait 0.5>>
 Magnolia: (This can't be it.)
 Magnolia: (I need to find a way.)
 <<wait 0.5>>
 Magnolia: (Do I need to go inside another dream?)
<<else>>
 Magnolia: (This can't be it.)
<<endif>>
===

title: BacktotheCity
---
<<if visited_count("BacktotheCity") < 1>>
 Magnolia: (Even if I wanted to go back to the city it seems like I can't.)
 Magnolia: (It's not walkable.)
<<else>>
 Magnolia: (I can’t go there.)
<<endif>>
//we are not able to pass here before we unlock the last DREAMWORLD
===

//ADDED NODES

title: TheCastle
---
Magnolia: (I wonder if the tied will come this far and ruin it.)
===

title: ChihiroInSand
---
Magnolia: (Who is Chihiro?)
===

title: TheManHole_Beach
---
<<SetPortrait noportrait>>
<<if visited_count("TheManHole_Beach") < 1>>
Magnolia: (I've barely seen this one.)
Magnolia: (of course, there is a word.)
 -> Read it.
  The Word: SUNLIGHT
  <<wait 0.2>>
  Magnolia: (What is it with the sun?)
  Magnolia: (It's everywhere except on the sky.)
<<else>>
 Magnolia: (The thief is in sunlight.)
 <<wait 1>>
 Magnolia: (I don't know what it means.)
<<endif>>
===